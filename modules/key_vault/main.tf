data "azurerm_client_config" "current" {}

resource "azurerm_key_vault" "kv" {
  name                          = var.key_vault_name
  location                      = var.location
  resource_group_name           = var.resource_group_name
  enabled_for_disk_encryption   = true
  tenant_id                     = var.tenant_id
  soft_delete_retention_days    = 7
  purge_protection_enabled      = true
  public_network_access_enabled = false
  sku_name                      = "standard"
  rbac_authorization_enabled    = true

  tags = { Env = var.env, Layer = "data ${var.env}" }
}

# Role Assignment for AKS to read secrets
resource "azurerm_role_assignment" "aks_kv_secrets_user" {
  scope                = azurerm_key_vault.kv.id
  role_definition_name = "Key Vault Secrets User"
  principal_id         = var.aks_managed_identity_principal_id
}


# Key Vault Private Endpoint
resource "azurerm_private_endpoint" "pe_kv" {
  name                = "pe-kv-${var.env}"
  location            = var.location
  resource_group_name = var.resource_group_name
  subnet_id           = var.pe_subnet_id

  private_service_connection {
    name                           = "psc-kv"
    private_connection_resource_id = azurerm_key_vault.kv.id
    is_manual_connection           = false
    subresource_names              = ["vault"]
  }

  private_dns_zone_group {
    name                 = "pdzg-kv"
    private_dns_zone_ids = [var.private_dns_zone_kv_id]
  }

  tags = { Env = var.env, Layer = "data ${var.env}" }
}

# Diagnostic settings for Key Vault -> Log Analytics
resource "azurerm_monitor_diagnostic_setting" "kv_diag" {
  name                       = "diag-kv-${var.env}"
  target_resource_id         = azurerm_key_vault.kv.id
  log_analytics_workspace_id = var.log_analytics_workspace_id

  enabled_log {
    category = "AuditEvent"
  }

  enabled_metric {
    category = "AllMetrics"
  }
}

